plugins {
    id('java')
    id('groovy')
    id('application')
    id("com.gradleup.shadow") version "9.0.2"
}

repositories {
    flatDir {
        dirs 'lib'
    }
    mavenCentral()
    mavenLocal()
}

application {
    mainModule = 'dse.cli.serial'
    mainClass = 'dse.cli.serial.Application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    modularity.inferModulePath.set(false)
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    implementation 'dse.libs:libdse-java:1.0.16'            // Handles the low-level telegrams sent by DSE sensors
    implementation 'com.fazecast:jSerialComm:[2.0.0,3.0.0)' // Used by libdse-java for cross-platform serial port comm.
}

jar {
    manifest {
        attributes(
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-User'     : System.properties['user.name'],
                'Build-Version'  : project.getVersion(),
                'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ").toString(),
                'Add-Opens'      : 'java.base/java.lang.invoke' // To ignore "Illegal reflective access by retrofit2.Platform" warnings
        )
    }
}
